plugins {
    id 'com.android.application'
}

android {

    def targetSDKVersion = 33

    namespace 'org.max.budgetcontrol'
    compileSdk 34

    def versionPropsFile = file('version.properties')
    def value = 0
    Properties versionProps = new Properties()
    if (!versionPropsFile.exists()) {
        versionProps['VERSION_PATCH'] = "0"
        versionProps['VERSION_NUMBER'] = "1"
        versionProps['VERSION_BUILD'] = "-1"
        // I set it to minus one so the first build is 0 which isn't super important.
        versionProps.store(versionPropsFile.newWriter(), null)
    }

    def runTasks = gradle.startParameter.taskNames
    if ('assembleRelease' in runTasks) {
        value = 1
    }

    def mApplicationName = "BudgetControl"
    def mVersionName = ""
    def mFileName = ""

    if (versionPropsFile.canRead()) {
        versionProps.load(new FileInputStream(versionPropsFile))

        versionProps['VERSION_PATCH'] = (versionProps['VERSION_PATCH'].toInteger() + value).toString()
        versionProps['VERSION_NUMBER'] = (versionProps['VERSION_NUMBER'].toInteger() + value).toString()
        versionProps['VERSION_BUILD'] = (versionProps['VERSION_BUILD'].toInteger() + 1).toString()

        versionProps.store(versionPropsFile.newWriter(), null)

        // 1: change major and minor version here
        mVersionName = "v${versionProps['VERSION_NUMBER']}.${versionProps['VERSION_PATCH']}.${versionProps['VERSION_BUILD']}"
        // 2: change AppName for your app name
        mFileName = "${mApplicationName}-${mVersionName}.apk"

    } else {
        throw new FileNotFoundException("Could not read version.properties!")
    }


    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            if (output.outputFile != null && output.outputFile.name.endsWith('.apk')) {
                outputFileName = mFileName
            }
        }
    }


    defaultConfig {
        applicationId "org.max.budgetcontrol"
        minSdk 33
        targetSdk targetSDKVersion
        versionCode 1
        versionName "1.0"
        versionCode versionProps['VERSION_NUMBER'].toInteger()
        versionName "${mVersionName} Build: ${versionProps['VERSION_BUILD']}"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildFeatures {
        viewBinding true
    }
}


dependencies {
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.10.0'
    implementation 'com.squareup.okhttp3:okhttp:3.12.0'
    implementation 'org.json:json:20231013'
    implementation 'androidx.preference:preference:1.2.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'net.mm2d.color-chooser:color-chooser:<version>'
    implementation project(':propeditors')

}